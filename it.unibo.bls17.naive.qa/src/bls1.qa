/*
 * This is A MODEL, result of problem analysis 
 * written in the CUSTOM meta-model/language qa
 */  
System bls0  
Dispatch turn     : switch   
Event local_click : clicked(N)  //N : natural

Context bls0Ctx ip [ host="localhost"  port=8029 ]    

QActor qaled context bls0Ctx {
Rules{
	ledmodel( name(led1), value(off) ).
	
	changeledValue( V ) :- 
		replaceRule( ledmodel( NAME,VALUE ), ledmodel( NAME,value(V) ) ).
}
	Plan init normal  [        
		javaRun it.unibo.custom.gui.customBlsGui.createCustomLedGui();
 		delay 100;
		[ !? ledmodel( NAME, value(V) )] javaRun it.unibo.custom.gui.customBlsGui.setLed(V)
    ]
   	switchTo waitForCmd   
   	 
  	Plan waitForCmd [ ]
    transition stopAfter 3000000
    	whenMsg turn -> ledswitch
    finally repeatPlan	
   
    //model-based behavior
    Plan ledswitch resumeLastPlan[
    	[ !? ledmodel( NAME, value(off) )] addRule newLedValue( on );
    	[ !? ledmodel( NAME, value(on) )]  addRule newLedValue( off );
    	[ ?? newLedValue(V)] demo changeledValue( V ) ;
    	[ !? ledmodel( NAME, value(V) )]   javaRun it.unibo.custom.gui.customBlsGui.setLed(V)
    ]
}

QActor qacontrol context bls0Ctx{
	Plan init normal [         
   		println( qacontrol(starts)  )  ;
   		javaRun it.unibo.custom.gui.customBlsGui.createCustomButtonGui()
   	]
   	switchTo waitForClick      
 
    Plan waitForClick[  ]  
    transition stopAfter 100000 
     	whenEvent local_click : clicked(N) do forward qaled -m turn : switch
    finally repeatPlan	
}