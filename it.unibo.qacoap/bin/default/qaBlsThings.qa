/* 
 * =======================================================================================
 * qaBlsThings.qa
 * =======================================================================================
 */    
System qaBlsThings                
Event dummy    : info(X)
Event usercmd  : usercmd(X)

Context ctxQaBlsThings ip [ host="localhost"   port=8032 ]  
 
/*
 * Activates the ButtonThing
 */ 
QActor qabuttonthing context ctxQaBlsThings -pubsub -g yellow {  
	Plan init normal [ 	
 		javaRun it.unibo.coap.platform.thingUtils.createButtonThing("8020")  ;
 		println("qabuttonthing STARTS")	 
 	]
	transition stopAfter 6000000
	whenEvent dummy : info(X) do println( info(X) )
 }
/*
 * Activates the LedThing
 */ 
QActor qaledthing context ctxQaBlsThings  -g cyan {  
	Plan init normal [ 	
 		javaRun it.unibo.coap.platform.thingUtils.createLedThing("8010")  ;
 		println("qaledthing STARTS") ;
 		delay 600000
  	]  
  } 
/*
 * Application logic:
 *   	interact with the LedThing when a usercmd event is perceived
 * TDODO: use MQTT and extend ButtonResourceGofObserver with some publish
 */
QActor qablslogic context ctxQaBlsThings   { 
	Plan init normal [ 	
  		println("qablslogic STARTS")  	
  	]   
	transition stopAfter 6000000
	whenEvent usercmd -> handleLedAsCoapClient
	finally repeatPlan
	
	Plan handleLedAsCoapClient resumeLastPlan [
		printCurrentEvent;	
		onEvent usercmd : usercmd(true)  -> javaOp "doCoapPost( \"localhost:8010/led\", \"true\" )";
		onEvent usercmd : usercmd(false) -> javaOp "doCoapPost( \"localhost:8010/led\", \"false\" )"
	]
	
}

/*
 * An actor that works as a ButtonThing client
 */
QActor qabuttonclient context ctxQaBlsThings   { 
	Plan init normal [ 	
  		println("qabuttonclient STARTS") ;
  		delay 1000;
  		javaOp "doCoapPost( \"localhost:8020/button\", \"true\" )";
  		delay 1000;
  		javaOp "doCoapPost( \"localhost:8020/button\", \"false\" )"
  	]   
  	finally repeatPlan 3
}