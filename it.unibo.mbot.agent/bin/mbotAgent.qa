/* 
 * ==============================================================
 * mbotAgent.qa
 * A robot that is able to move and to react to events
 * ==============================================================
 */     
System mbotAgent     
Event usercmd      : usercmd(CMD)     
Event alarmev      : alarm( X )   
Event sonarSensor  : sonar( NAME, DISTANCE )	//From rover or real robot

Dispatch alarmmsg  : alarm( X ) 	//same payload as alarmev
Dispatch moveRover : cmd( CMD )

pubSubServer  "tcp://192.168.43.229:1883"    

Context ctxMbotAgent ip [ host="192.168.43.229"   port=8039 ]    //192.168.43.229
EventHandler evh for alarmev -print {	//event-driven
	forwardEvent roveragent -m alarmmsg 
};  

QActor roveragent context ctxMbotAgent -pubsub{      
Rules{
	foundObstacle :- retract( sonar(roversonar, D) ), eval( lt, D, 20 ).
} 	     
	Plan init normal [  
		println("rovercontroller STARTS") 		
	]  
	switchTo lookAtSonars	     
	Plan lookAtSonars[    
 		println("lookAtSonars")   
 	] 
	transition   stopAfter 6000000     
		whenMsg  alarmmsg       -> alarmHandlePolicy,
 		whenEvent  sonarSensor  -> handleSonarEvents   	 
	finally repeatPlan
	
	Plan alarmHandlePolicy resumeLastPlan[ 
		printCurrentMessage;  
		println("ALARM HANDLING POLICY ... ") ;
		emit usercmd : usercmd( robotgui(h(low)) ) ;            
		println("ALARM HANDLING POLICY DONE ") 
	]

	Plan handleSonarEvents resumeLastPlan[
		printCurrentEvent;
		onEvent sonarSensor : sonar(realsonar, DISTANCE) -> {
			//virtual obstacle
			println(  roversonar  );
			emit usercmd :  usercmd( robotgui(s(low)) ) ; //retrogress
			delay 700 ;    //continue to move to stop sonar events    
			emit usercmd : usercmd( robotgui(h(low)) ) ; 		 	   
			delay 1500   ;  //in this time, an alarm could be emitted
			emit usercmd :  usercmd( robotgui(s(low)) )  //retrogress and stop if alarm			
		}; 
		onEvent sonarSensor : sonar(roversonar, DISTANCE) -> {
			//virtual obstacle
			println(  roversonar  );
			emit usercmd :  usercmd( robotgui(s(low)) ) ; //retrogress
			delay 700 ;    //continue to move to stop sonar events    
			emit usercmd : usercmd( robotgui(h(low)) ) ; 		 	        
			println("%%%%%%%%%%%%%%%%%%%%%%%%%%%%% LONG ACTION");        
			//in this time an alarm could be emitted  
  		emit usercmd :  usercmd( robotgui(a(low)) )	;    //750 msec
  		emit usercmd :  usercmd( robotgui(a(low)) )	;    //750 msec
  		emit usercmd :  usercmd( robotgui(a(low)) )	;    //750 msec  
  		emit usercmd :  usercmd( robotgui(a(low)) )	;    //750 msec 			   
		emit usercmd :  usercmd( robotgui(s(low)) ) ;    //retrogress 	
			println("%%%%%%%%%%%%%%%%%%%%%%%%%%%%% LONG ACTION END")   
 		};
		onEvent sonarSensor : sonar(sonar1, DISTANCE) -> {
			//fixed sonar (moving ahead or backwards)
			println( sonar1 );    //no variable substitution, sorry
			delay 500 ;    //continue to move to stop sonar events  
			emit usercmd : usercmd( robotgui(h(low)) ) 		 	   			
		};		
		onEvent sonarSensor : sonar(sonar2, DISTANCE) -> {
			//fixed sonar (moving ahead or backwards)
			println( sonar2 );    //no variable substitution, sorry
			delay 500 ;    //continue to move to stop sonar events  
			emit usercmd : usercmd( robotgui(h(low)) ) 		 	          			
		}
 	]
}//roveragent
  

 
 		
