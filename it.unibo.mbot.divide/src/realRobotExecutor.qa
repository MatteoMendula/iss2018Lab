/* 
 * =======================================================================================
 * realRobotExecutor.qa
 * =======================================================================================
 */        
System realRobotExecutor            
Event usercmd     : usercmd(CMD)  					//from web gui
Event sonar  	  : sonar(SONAR, TARGET, DISTANCE)	//From (virtual) sonar
Event sonarDetect : sonarDetect(X)					//From (virtual robot) sonar
Event realSonar   : sonar( DISTANCE )				//From real sonar on real robot
Event    polar    : p( Distance, Angle )  
Event mindcmd     : usercmd(CMD) 				//from the appl agent (the 'mind')
Event mbotcmd     : usercmd(CMD)    

Dispatch moveMbot : usercmd( CMD )
  
Context ctxRealRobotExecutor ip [ host="192.168.43.67"   port=8029 ]  -httpserver     
EventHandler evh for usercmd , mindcmd -print {	//event-driven
 	forwardEvent mbot -m moveMbot  	//from event to message     
};
Context ctxRadarBase    ip [ host="192.168.43.229"   port=8033 ]  -standalone 	 
  
QActor mbot context ctxRealRobotExecutor {   
	Rules{	  
  		foundObstacle :- retract( realDistance(D) ), eval( lt, D, 20 ).
	}
	Plan init normal [       
 	    javaRun it.unibo.mbot.mbotConnArduino.initRasp()	;
	    println("mbot START")    
  	] 
    switchTo waitForCmd        
		
   	Plan waitForCmd[  ]  
  	transition stopAfter 3600000   	//1h 
 		whenMsg moveMbot   -> execMove 
   	finally repeatPlan    
	   
  	Plan execMove resumeLastPlan[     
  		printCurrentMessage;
     	onMsg moveMbot : usercmd( robotgui(h(X)) )  -> javaRun it.unibo.mbot.mbotConnArduino.mbotStop();  
  		onMsg moveMbot : usercmd( robotgui(w(X)) )  -> javaRun it.unibo.mbot.mbotConnArduino.mbotForward();    
    	onMsg moveMbot : usercmd( robotgui(s(X)) )  -> javaRun it.unibo.mbot.mbotConnArduino.mbotBackward(); 
  		onMsg moveMbot : usercmd( robotgui(a(X)) )   -> {
  			javaRun it.unibo.mbot.mbotConnArduino.mbotLeft();
			delay 680;	//TODO: use some configuration parameter
 			javaRun it.unibo.mbot.mbotConnArduino.mbotStop()				
  		};    
   		onMsg moveMbot : usercmd( robotgui(d(X)) )  -> {
			javaRun it.unibo.mbot.mbotConnArduino.mbotRight();
			delay 680;//TODO: use some configuration parameter
	 		javaRun it.unibo.mbot.mbotConnArduino.mbotStop()   			
   		}  ;   
    	onMsg moveMbot : usercmd( robotgui(f(X)) )  -> javaRun it.unibo.mbot.mbotConnArduino.mbotLinefollow()     
   	]           
}  

/*
 * -----------------------------------------------------
 * Convert sonar events into polar events
 * -----------------------------------------------------
 */
QActor sonardetector context ctxRealRobotExecutor{
	Plan init normal [
		println("sonardetector STARTS ")
	]
	switchTo waitForEvents
	
	Plan waitForEvents[ 
		println("sonardetector WAITS ")
	]    
  	transition stopAfter 3600000   	//1h     
 		whenEvent sonar  -> handleVirtualSonar,
  		whenEvent sonarDetect : sonarDetect(X)  do println( sonarDetect(X) ),
 		whenEvent realSonar   ->  handleRealSonar
  	finally repeatPlan  
	  
  	Plan handleRealSonar resumeLastPlan[ 
 		printCurrentEvent; 		
		onEvent realSonar   : sonar( DISTANCE ) -> emit polar : p(DISTANCE, 0) 
 	]
  	Plan handleVirtualSonar resumeLastPlan[ 
 		printCurrentEvent
	]
 }
